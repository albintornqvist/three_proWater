<!DOCTYPE html>

<html>

<head>
    <title>The Procedural Ocean</title>
    <script type="text/javascript" src="libs/three.js"></script>
    <script type="text/javascript" src="libs/stats.js"></script>
    <script type="text/javascript" src="libs/simplex-noise.js"></script>
    <script type="text/javascript" src="libs/dat.gui.js"></script>
    <style>
        body {
            /* set margin to 0 and overflow to hidden, to go fullscreen */
            margin: 0;
            overflow: hidden;
        }
    </style>
</head>
<body>

<div id="Stats-output">
</div>

<!-- Div which will hold the Output -->
<div id="WebGL-output">
</div>

<script type="x-shader/x-vertex" id="vertexShader">

// Description : Array and textureless GLSL 2D/3D/4D simplex noise
//               functions.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : ijm
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//
vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec2 mod289(vec2 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 permute(vec3 x) {
  return mod289(((x*34.0)+1.0)*x);
}

float snoise(vec2 v) {
    const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                    0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                   -0.577350269189626,  // -1.0 + 2.0 * C.x
                    0.024390243902439); // 1.0 / 41.0
  // First corner
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);

  // Other corners
  vec2 i1;
  //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
  //i1.y = 1.0 - i1.x;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  // x0 = x0 - 0.0 + 0.0 * C.xx ;
  // x1 = x0 - i1 + 1.0 * C.xx ;
  // x2 = x0 - 1.0 + 2.0 * C.xx ;
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;

  // Permutations
  i = mod289(i); // Avoid truncation effects in permutation
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
    + i.x + vec3(0.0, i1.x, 1.0 ));

  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;

  // Gradients: 41 points uniformly over a line, mapped onto a diamond.
  // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;

  // Normalise gradients implicitly by scaling m
  // Approximation of: m *= inversesqrt( a0*a0 + h*h );
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

  // Compute final noise value at P
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}

vec4 mod289(vec4 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x) {
     return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r) {
  return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v, out vec3 gradient) {
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

  // First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

  // Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
  //   x1 = x0 - i1  + 1.0 * C.xxx;
  //   x2 = x0 - i2  + 2.0 * C.xxx;
  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

  // Permutations
  i = mod289(i);
  vec4 p = permute( permute( permute(
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

  // Gradients: 7x7 points over a square, mapped onto an octahedron.
  // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
  float n_ = 0.142857142857; // 1.0/7.0
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

  //Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

  // Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  vec4 m2 = m * m;
  vec4 m4 = m2 * m2;
  vec4 pdotx = vec4(dot(p0,x0), dot(p1,x1), dot(p2,x2), dot(p3,x3));

  // Determine noise gradient
  vec4 temp = m2 * m * pdotx;
  gradient = -8.0 * (temp.x * x0 + temp.y * x1 + temp.z * x2 + temp.w * x3);
  gradient += m4.x * p0 + m4.y * p1 + m4.z * p2 + m4.w * p3;
  gradient *= 42.0;

  return 42.0 * dot(m4, pdotx);
}

    const float DEG_TO_RAD = 3.141592653589793 / 180.0;

    mat4 rotationMatrix(vec3 axis, float angle)
    {
        axis = normalize(axis);
        float s = sin(angle);
        float c = cos(angle);
        float oc = 1.0 - c;

        return mat4(oc * axis.x * axis.x + c,           oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
                    oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           oc * axis.y * axis.z - axis.x * s,  0.0,
                    oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           0.0,
                    0.0,                                0.0,                                0.0,                                1.0);
    }

    float smoothAbs(float n) {
        float mu = 0.07;
        return abs(n) < mu ? n*n/(2.0*mu)*0.5 : abs(n)-mu*0.5;
    }

    float waterHeightMap(vec2 p, float time) {

      vec2 tempPosition = (rotationMatrix(vec3(0.0, 0.0, 1.0), 160.0 * DEG_TO_RAD) * vec4(vec3(p, 0.0), 1.0)).xy;
      vec3 position = vec3(tempPosition.x, tempPosition.y * 2.0 - time * 9.0, time * 9.0);

      vec3 grad = vec3(0.0);

      float firstWave = 9.0 * snoise(0.01 * position, grad); // Just to get some large movement

      vec3 pStretched = vec3(tempPosition.x, tempPosition.y * 3.0 - 2.0 * time, 2.0 * time);
      float secondWave = 2.0 * -smoothAbs(snoise(0.04 * pStretched, grad));

      vec3 pStretched2 = vec3(tempPosition.x, tempPosition.y * 3.0 - time, time);
      float thirdWave = 0.5 * -smoothAbs(snoise(0.08 * pStretched2, grad));

      vec3 pStretched3 = vec3(tempPosition.x, tempPosition.y * 3.0 - time, time);
      float fourthWave = 0.1 * -smoothAbs(snoise(0.16 * pStretched3, grad));

      vec3 pStretched4 = vec3(tempPosition.x, tempPosition.y * 3.0 - 2.0 * time, 2.0 * time);
      float fifthWave = 9.0 * -smoothAbs(snoise(0.01 * pStretched4, grad));

      return firstWave + secondWave + thirdWave + fourthWave + fifthWave;
    }

    float cutoffFilter(float min, float max, float displacement) {
      if (displacement < max && displacement > min) {
        return 1.0;
      } else {
        return 0.0;
      }
    }

    float foamMap(vec3 p, float time) {
      vec2 rotatedPos = (rotationMatrix(vec3(0.0, 0.0, 1.0), 160.0 * DEG_TO_RAD) * vec4(p, 1.0)).xy;
      vec3 position = vec3(rotatedPos.x, rotatedPos.y - time, time * 0.1);
      vec3 grad = vec3(0.0);



      float secondWave = (snoise(0.5 * position, grad) + 5.0 * snoise(1.0 * position, grad))/2.0;

      float firstWave = 3.0 * snoise(0.04 * vec3(position.x + secondWave, (position.y + secondWave)*3.0, position.z), grad) + 3.0 * snoise(0.02 * vec3(position.x + secondWave, (position.y + secondWave)*3.0, position.z), grad);

      float displacement = cutoffFilter(0.1, 1.0, firstWave);
      return displacement;
    }

    //Vertex shader
    varying float noise;
    varying vec3 vNormal;
    varying vec3 vPosition;
    varying float foam;
    uniform float time;

    void main() {
        float displacement = waterHeightMap(position.xy, time);
        // move the position along the normal and transform it
        vec3 newPosition = position + normal * displacement;

        //Recalculate normal
        float delta = 0.0003;
        vec3 diffX = vec3(position.x + delta, position.y, position.z);
        vec3 diffY = vec3(position.x, position.y + delta, position.z);
        vec3 posX = diffX + normal * waterHeightMap(diffX.xy, time);
        vec3 posY = diffY + normal * waterHeightMap(diffY.xy, time);
        vec3 newNormal = normalize(cross(normalize(posX - newPosition), normalize(posY - newPosition)));

        //share the normal and position with the fragment shader
        vNormal = newNormal;
        vPosition = newPosition;
        foam = foamMap(newPosition, time);

        gl_Position = projectionMatrix * modelViewMatrix * vec4( vPosition, 1.0 );
    }
</script>

<script type="x-shader/x-vertex" id="fragmentShader">

    float clipFoam(float foam, float height, vec3 normal) {
      if (height > 0.0) {
        return (foam * height / 8.0);
      }
      return 0.0;
    }

    vec3 lightFromHeight(float height) {
      // if (height > 0.0) {
        return vec3(0.5, 0.6, 0.8) * (height + 9.0) / 20.0;
      // }
      // return vec3(0.0, 0.0, 0.0);
    }

    varying float noise;
    varying vec3 vNormal;
    varying vec3 vPosition;
    varying float foam;

    void main() {

        vec3 lightDirection = normalize(vec3(0.0, 0.7, 1.0));

        float lightA = 0.01;  // Ambient level for diffuse reflection

        float phongD = max(0.0, dot(lightDirection, vNormal));

        vec3 reflDir = normalize(2.0 * dot(lightDirection, vNormal) * vNormal - lightDirection);
        vec3 viewDir = normalize(vPosition - cameraPosition);
        float lightS = max(0.0, dot(reflDir, viewDir));
        lightS = 0.4 * pow(lightS, 6.0);

        vec3 totalColor = phongD * vec3(0.2, 0.4, 0.6) + lightS * vec3(1.0, 1.0, 1.0) + 0.5 * clipFoam(foam, vPosition.z, vNormal) * vec3(1.0, 1.0, 1.0) + 0.5 * lightFromHeight(vPosition.z);

        gl_FragColor = vec4( totalColor, 1.0 );
    }

</script>

<script type="text/javascript">

    var camera;
    var scene;
    var renderer;

    var attributes = {
          displacement: {
            type: 'f', // a float
            value: [] // an empty array
          }
        };
    var uniforms = {
      time: {
        type: 'f',
        value: 0
      },
      amplitude: {
        type: 'f', // a float
        value: 0
      }
    };

    // once everything is loaded, we run our Three.js stuff.
    function init() {

        var stats = initStats();

        // create a scene, that will hold all our elements such as objects, cameras and lights.
        scene = new THREE.Scene();

        // scene.fog = new THREE.FogExp2( "#DDDDDD", 0.002 );

        // create a camera, which defines where we're looking at.
        camera = new THREE.PerspectiveCamera(45, window.innerWidth / window.innerHeight, 0.1, 1000);

        // create a render and set the size
        renderer = new THREE.WebGLRenderer();
        renderer.setClearColor(new THREE.Color(0xEEEEEE));
        renderer.setSize(window.innerWidth, window.innerHeight);
        renderer.shadowMapEnabled = true;

        // show axes in the screen
        var axes = new THREE.AxisHelper(20);
        scene.add(axes);

        // GEOMETRIES
        var planeGeometry = new THREE.PlaneGeometry(120, 120, 400, 400);
        var shaderMaterial = new THREE.ShaderMaterial({
            uniforms: uniforms,
            vertexShader: document.getElementById( 'vertexShader' ).textContent,
            fragmentShader: document.getElementById( 'fragmentShader' ).textContent
        });
        var plane = new THREE.Mesh(planeGeometry, shaderMaterial);
        plane.receiveShadow = true;

        // rotate and position the plane
        plane.rotation.x = -0.5 * Math.PI;
        plane.position.x = 0;// 10;
        plane.position.y = 0;
        plane.position.z = 0;

        // add the plane to the scene
        scene.add(plane);


        // var sphereGeometry = new THREE.SphereGeometry(4, 20, 20);
        // var sphereMaterial = new THREE.MeshLambertMaterial({color: 0x7777ff});
        // var sphere = new THREE.Mesh(sphereGeometry, sphereMaterial);
        //
        // // position the sphere
        // sphere.position.x = 0;
        // sphere.position.y = 1;
        // sphere.position.z = 0;
        // sphere.castShadow = true;
        //
        // // add the sphere to the scene
        // scene.add(sphere);


        // position and point the camera to the center of the scene
        camera.position.x = -70;
        camera.position.y = 40; // up
        camera.position.z = 70;
        camera.lookAt(scene.position);

        //LIGHTS
        // var ambientLight = new THREE.AmbientLight(0x404040, 1);
        // scene.add(ambientLight);

        // add spotlight for the shadows
        // var spotLight = new THREE.SpotLight(0xffffff);
        // spotLight.position.set(-50, 40, -10);
        // spotLight.castShadow = true;
        // scene.add(spotLight);
        //
        // var light = new THREE.PointLight( 0x404040, 1, 0 );
        // light.position.set( -50, 40, -40 );
        // scene.add( light );

        //CONTROLS
        var controls = new function () {
            this.amplitude = 5;
            this.frequency = 0.05;
            this.sphereHeight = 0;
        };

        var gui = new dat.GUI();
        gui.add(controls, 'amplitude', 0, 50);
        gui.add(controls, 'frequency', 0, 0.1);
        gui.add(controls, 'sphereHeight', 0, 10);

        // add the output of the renderer to the html element
        document.getElementById("WebGL-output").appendChild(renderer.domElement);

        // call the render function
        var step = 0;


        //SAVED STATES
        // var sphereYPos = sphere.position.y;

        renderScene();

        function renderScene() {
            stats.update();

            uniforms.time.value = step;
            // bounce the sphere up and down
            uniforms.amplitude.value = step;

            step += 0.1;

            renderer.render(scene, camera);
            // render using requestAnimationFrame
            requestAnimationFrame(renderScene);
        }

        function initStats() {

            var stats = new Stats();

            stats.setMode(0); // 0: fps, 1: ms

            // Align top-left
            stats.domElement.style.position = 'absolute';
            stats.domElement.style.left = '0px';
            stats.domElement.style.top = '0px';

            document.getElementById("Stats-output").appendChild(stats.domElement);

            return stats;
        }
    }
    function onResize() {
        camera.aspect = window.innerWidth / window.innerHeight;
        camera.updateProjectionMatrix();
        renderer.setSize(window.innerWidth, window.innerHeight);
    }


    window.onload = init;

    // listen to the resize events
    window.addEventListener('resize', onResize, false);

</script>
</body>
</html>
